# Классы и структуры

## Классы

Классы в D очень похожи на классы в Java. Так же запрещено множественное наследование,
все классы наследуются от класса Object, а так же они являются ссылочным типом
и всегда создаются в куче.

**Простой класс** на D:

```d
// в D по умолчанию модификаторы доступа и наследования всегда 'public'
class MyClass {

}
```

**Класс, который нельзя наследовать** на Java и D будет реализован абсолютно одинаково:

```java
final class MyClass {
    // также D поддерживает final функции, которые нельзя переопределить
    final void foo() {
        // ...
    }
}
```

**Абстрактные классы**, экземпляры которых нельзя создать, и которые можно только наследовать, так же не будут отличаться:

```d
abstract class MyClass {
    // Абстрактные методы также идентичные
    abstract void someMethod(); 
}
```

**Вложенные классы** так же не отличаются:

```d
class MyClass {
    // Обязательно должен быть static, чтоб избежать утечек памяти
    static class InnerClass {

    }
}
```

#### Наследование классов

```d
// Вместо "extends" используем ":"
class SubClass : SuperClass {

    // Для обозначения конструктора используем "this" вместо имени класса
    this() {
        // обязательно должны вызвать любой
        // сучествующий родительский конструктор
        super(5);
    }
    
    // Переопределяем метод родительського класса.
    // Вместо @Override используем ключевое слово "override".
    override int calculate() {
        return 2 + 2;
    }
}

class SuperClass {

    // private - классы-наследники не будут видеть переменную
    // immutable - аналог final, значит переменная обязательно
    // должна быть проинициализированна в конструкторе
    private immutable int value;

    this(int value) {
        this.value = value;
    }

    int calculate() {
        return value * 2;
    }
}
```

Создание экземпляра класса:

```d
MyClass myClass;
// ключевым словом "is" сравниваем обьекты на null
assert(myClass is null);
// если нет параметров конструктора, "()" можно не писать
myClass = new MyClass;
// доступ к публичным методам и полям также получаем через точку
myClass.foo(12);
```

Создать класс в стеке можно [средствами стандартной библиотеки](https://dlang.org/phobos/std_typecons.html#scoped).

Все методы класса в D по умолчанию виртуальные, а для переопределения
в наследнике нужно использовать ключевое слово `override`. Не являются
виртуальными только приватные и помеченные как `final` методы.

Отсутствие множественного наследования, как и в Java, компенсируется механизмом реализации
интерфейсов. Концепция интерфейсов схожа с интерфейсами в Java, но допускается реализация `final` методов.

```d
import std.stdio;

interface Foo { int func1(int x); }

interface Bar { int func2(int x); }

class Base
{
    void func3() { writeln(__FUNCTION__); }
    void func4(int x) { writeln(x^^3); }
}

class E : Base, Foo, Bar
{
    override // можно группировать атрибуты
    {
        int func1(int x) { return x * 2; }
        int func2(int x) { return x * x; }
    }

    override void func4(int x) { writeln(x^^4); }
}
```

## Структуры

Язык программирования D поддерживает кроме классов также [структуры](https://dlang.org/spec/struct.html).
Структуры в D нельзя наследовать. Они передаются по значению и создаются в стеке.
Все методы структур не виртуальны (т.к. запрещено наследование).

```d
import std.stdio : writeln;

struct Test {
    int x;
    int y;

    void addX(int _x) {
        x = _x;
    }

    void addY(int _y) {
        y = _y;
    }
}

void main() {
    // В структуре можно задавать начальное значение полей в конструкторе,
    // придерживаясь порядка определения (в нашем случае первое значение X,
    // второе Y)
    // При создании екземпляра структуры не надо использовать ключевое слово
    // "new", так как структура не ссылочный тип.
    Test test = Test(0, 0);
    writeln(test); // Test(0, 0)
    
    // Будет создана новая структура
    Test newTest = test;
	
    test.addX(1);
    test.addY(-10);
    writeln(test); // Test(1, -10)
    writeln(newTest); // Test(0, 0) - значения второй структуры не изменились
}
```
